*9/16/86 Clean up the parsing of XFER'd files.                   [1.24] 00001338
*        This REVERSES the meaning of S#NXFER SITEOPT.           [1.24] 00001346
*        Add call to CNTRCVD to check hop count.                 [1.24] 00001354
*[1.23]Take Reg Quinton's mod for INCOMING table;  it now works  [1.23] 00001362
*      for a local user as well as from net. This mod is done    [1.23] 00001370
*      slightly differently from Reg's.                          [1.23] 00001410
*[1.22] Check for RSCS-domain as part of a valid host name       [1.22] 00001450
*[1.18] Added test for transferred files                                00001580
*[1.16] Added support for Resent- fields.                               00002000
*        also change AT to @                                            00003000
VALIDATE TITLE 'CUCCA VM NETWORK MAILER - VERSION 1 - MAIL FILE SENDER *00004000
                VALIDATION'                                             00005000
*---------------------------------------------------------------------* 00006000
*      MAILER -- Copyright (c) 1982,1985 Columbia University.         * 00007000
*    Program Property of the Trustees of Columbia University in the   * 00008000
*    City of New York.                                                * 00009000
*---------------------------------------------------------------------* 00010000
VALIDATE CSECT ,                                                        00011000
* VALIDATE -                                                            00012000
*  Validate checks to see that the minimum required fields are          00013000
*  present in the message header.  These are "Date:" and                00014000
*  "From:" or "Sender:" and "From:".   In addition, it checks           00015000
*  that the following fields appear no more than once (other            00016000
*  fields may be repeated -- although it looks stupid):                 00017000
*  "Date:", "From:", "Sender:", "Reply-to:" [RFC733 iii.c]              00018000
*  it also verifies that the sender of the mail file is                 00019000
*  authorized to send mail and is not sending mail under an             00020000
*  assumed name.  this function is done differently depending           00021000
*  on the mail file source:                                             00022000
*        (1) File is from a local user (mflg1lcl is on).                00023000
*        the "Sender:", or "From:" field is checked to see if           00024000
*        the userid corresponds with the userid found in the            00025000
*        spool file block.                                              00026000
*        (2) File is from a network mailer (mflg1lcl is off).           00027000
*        the sending node name is looked up in the routing table.       00028000
*        the vm userid of the sending user (from rscs tag) must         00029000
*        correspond to the name of the mailer at that node. it          00030000
*        is assumed that that mailer has done validity checking         00031000
*        of mail that it is forwarding.                                 00032000
*        if the allow-unknown-nodes option is on, then unknown          00033000
*        network nodes can send files to mailer.  an optional           00034000
*        validity exit named *default* will be invoked for these        00035000
*        nodes if it is supplied.                                       00036000
*[1.16]  First check for RESENT- fields and do similar processing       00037000
*[1.16]  as above                                                       00038000
*1.16    NOTE:  Although RFC822 states that multiple occurences of      00039000
*1.16    a RESENT field is undefined,  the DEC-20 implementation        00040000
*1.16    allows a file to be resent multiple times.  To handle this,    00041000
*1.16    only pay attention to the last "group" of resent fields        00042000
*1.16    where group "n" is defined by the Nth occurence of a           00043000
*1.16    resent-date.  It is considered an error to have more           00044000
*1.16    resent field repetitions than the number for the resent-date   00045000
*1.16    field.  (4 Resent-froms and 3 resent-dates is illegal while    00046000
*1.16    3 of each is legal.  And 3 froms and 4 dates is OK and means   00047000
*1.16    there is no resent-from field for the last resent group).      00048000
*  SPECIAL CASE:                                                        00049000
*        If the file was generated by this mailer (i.e. userid *)       00050000
*        then it may be from one of three sources:                      00051000
*          a) MAILRET is returning an error message.                    00052000
*          b) RSCS rejected a file and transferred it back.             00053000
*          c) A user transferred a file back.                           00054000
*        Case (a) can be checked since MAILRET sets the tag to          00055000
*        read "--- returned mail file ---".                             00056000
*        Case (b) will contain the tag "<nodename> <userid>"            00057000
*        which Mailer just put into it when punching the file.          00058000
*        Case (c) should have no tag.  However the user may             00059000
*        tag the file prior to transferring it back.                    00060000
*                                                                       00061000
*                                                                       00062000
* LAST EDIT: 5/23/82 BY ALAN CROSSWELL                                  00063000
*                                                                       00064000
* CALLED VIA - CALL VALIDATE,(MAILBLOK)                                 00065000
*    MAILBLOK=ADDRESS OF MAILBLOK MACRO EXPANSION                       00066000
PAB      DSECT ,                                                        00067000
@MBLOK   DS    A             ADDRESS OF MAILBLOK                        00068000
         SPACE ,                                                        00069000
*                                                                       00070000
         SPACE                                                          00071000
* REGISTER USAGE -                                                      00072000
R0  EQU  0 -                                                            00073000
R1  EQU  1 -                                                            00074000
R2  EQU  2 -                                                            00075000
R3  EQU  3 -                                                            00076000
R4  EQU  4 -                                                            00077000
R5  EQU  5 -                                                            00078000
R6  EQU  6 -                                                            00079000
R7  EQU  7 -                                                            00080000
R8  EQU  8 - VALD BASE                                                  00081000
R9  EQU  9 - STACK POINTER                                              00082000
R10 EQU 10 - STACK LENGTH                                               00083000
R11 EQU 11 - MAILBLOK BASE                                              00084000
R12 EQU 12 - PROGRAM BASE                                               00085000
R13 EQU 13 - SAVE AREA                                                  00086000
R14 EQU 14 - SUBROUTINE LINKAGE                                         00087000
R15 EQU 15 - SUBROUTINE LINKAGE                                         00088000
         SPACE                                                          00089000
         PRINT NOGEN                                                    00090000
         BLOCKS ,                                                       00091000
         MAILBLOK ,                                                     00092000
         EJECT                                                          00093000
VALIDATE CSECT ,                                                        00094000
         USING VALIDATE,R15  TEMPORARY ADDRESSABILITY                   00095000
         B     EYESKIP       BRANCH OVER EYESKIP                        00096000
MYNAME   DC    CL8'VALIDATE',CL8'&SYSDATE'                              00097000
EYESKIP  DS    0H                                                       00098000
         DROP  R15                                                      00099000
         STM   R14,R12,12(R13) SAVE CALLER'S REGS                       00100000
         LR    R12,R15       PERMANENT ADDRESSABILITY                   00101000
         USING VALIDATE,R12                                             00102000
         LA    R14,SAVE      MY SAVE AREA                               00103000
         ST    R13,4(R14)    SAVE CALLER'S PTR                          00104000
         ST    R14,8(R13)    CHAIN MINE BACK                            00105000
         LR    R13,R14       SET MY SAVE PTR                            00106000
         USING PAB,R1        ADDRESS PARMS                              00107000
         L     R11,@MBLOK    GET MAILBLOK BASE                          00108000
         DROP  R1                                                       00109000
         USING MAILBLOK,R11  GET MAILBLOK ADDRESSABILITY                00110000
         LM    R9,R10,STACKP GET STACK POINTERS                         00111000
         NI    MAILFLG1,X'FF'-MFLG1RES [1.16]                           00112000
         SPACE ,                                                        00113000
***begin [1.18]------------------------------------------------------   00114000
*        Check if the file was transferred to me and disallow this      00115000
*        ONLY if the site options say so.                               00116000
*[1.24] REVERSE meaning of S#NXFER.                              [1.24] 00116500
*--------------------------------------------------------------------   00117000
         TM    SITEOPTS,S#NXFER  ENABLE this checking selected?  [1.24] 00118590
         BZ    SKIPXFER      don't do the xfer check                    00119180
         LA    R1,MAILSFB    get the sfblok                             00120000
         USING SFBLOK,R1     address it                                 00121000
         TM    SFBFLAG3,SFBXFER  was it transferred?                    00122000
         BO    BADXFER       kill this one.                             00123000
         DROP  R1            done with SFBLOK                           00124000
SKIPXFER EQU   *             here when xfer doesn't get checked         00125000
*end [1.18]----------------------------------------------------------   00126000
         SPACE ,                                                        00127000
*--------------------------------------------------------------------   00128000
*  check the special case that the file is locally generated by         00129000
*  this mailer.  If it is,  it can have one of 3 tags/xfer status[1.24] 00130050
*            tag                       xfer status               [1.24] 00130100
*     ==========================       ==========================[1.24] 00130150
*  1) --- returned mail file ---       NOT xfer'd (direct from *)[1.24] 00130200
*  2) FILE (....) ORIGIN ...           xfer'd (from recipient)   [1.24] 00130250
*  3) nodename userid                  xfer'd (from RSCS)        [1.24] 00130300
*                                                                [1.24] 00130350
*  (1) means I am punching a file back to self for handling.     [1.24] 00130400
*  (2) means a (dumb) user transferred this file back to MAILER  [1.24] 00130450
*  (3) means RSCS transferred it back because the nodename       [1.24] 00130500
*      is not known by RSCS.                                     [1.24] 00130550
*  Note that (2) or (3) can be counterfeited by a recipient user [1.24] 00130600
*  who explicitly sets the TAG of the file and then transfers it [1.24] 00130650
*  back to mailer.  Hence,  the more paranoid S#NXFER option.    [1.24] 00130700
*--------------------------------------------------------------------   00131000
         TM    MAILFLG1,MFLG1LCL       IS IT LOCAL?                     00132000
         BNO   NOTSPCL       NOT THE SPECIAL CASE                       00133000
         CLC   MAILUSER,MAILVMID  FILE ORIG AND SELF THE SAME?          00134000
         BNE   NOTSPCL       NOT THE SPECIAL CASE                       00135000
* (1) Check for XFER.  Should always fail for pre-SP2 CMS which  [1.24] 00135100
*     is OK.  We will just fall thru for pre-SP2 sites....       [1.24] 00135200
         LA    R1,MAILSFB    get the sfblok                      [1.24] 00135300
         USING SFBLOK,R1     addressability                      [1.24] 00135400
         TM    SFBFLAG3,SFBXFER  was it transferred?             [1.24] 00135500
         BO    SPCL2         -> yes, so it can't be case (1)     [1.24] 00135600
         DROP  R1            see if it is really case (1):       [1.24] 00135700
         CLC   =C'--- returned mail file ---',MAILTAG                   00136000
         BE    NOTSPCL       -> In this case, I trust me.        [1.24] 00137390
SPCL2    EQU   *             <- XFER'd or drop down thru above.  [1.24] 00137780
* (2) Check for user XFER.                                       [1.24] 00138170
         CLC   =C'FILE (',MAILTAG  user xfer'd back?             [1.24] 00138560
         BE    BADUXFER      -> bad user xfer.  sigh.            [1.24] 00138950
* (3) ASSUME THE FILE WAS REJECTED BY RSCS                       [1.24] 00139340
         LA    R5,MAILTAG    ADDRESS OF NODENAME                        00140000
         WTR   VAL036,(CA,(R5))   WRITE THE MESSAGE              [1.24] 00141990
         OI    MAILFLG1,MFLG1REJ SET REJECTION FLAG                     00143000
NOTSPCL  EQU   *             END OF SPECIAL CASE                        00144000
         SPACE ,                                                        00145000
*----------------------------------------------------------------[1.24] 00145090
*  Count the number of "Received:" headers and fail if greater   [1.24] 00145180
*  than MAXRCVD.  If MAXRCVD == 0,  skip this check.             [1.24] 00145270
*----------------------------------------------------------------[1.24] 00145360
         CLC   MAXRCVD,=F'0' not doing hop count checks?         [1.24] 00145450
         BE    RCVDOK        -> skip hop count call              [1.24] 00145540
         CALL  CNTRCVD       count number of Received headers    [1.24] 00145630
         C     R15,MAXRCVD   retcode is number seen (or zero)    [1.24] 00145720
         BH    HOPCOUNT      -> hop max exceeded                 [1.24] 00145810
RCVDOK   EQU   *             <- here when hop count bypassed     [1.24] 00145900
*--------------------------------------------------------------------   00146000
*        CHECK FOR THE EXISTENCE OF THE REQUIRED FIELDS:                00147000
*        "DATE:", "FROM:" OR "FROM:" AND "SENDER:"                      00148000
*--------------------------------------------------------------------   00149000
         SPACE ,                                                        00150000
         XC    MAILDFLD,MAILDFLD [1.16]zero date ptr until we see it.   00151000
*[1.16]check for Resent-Date first.                                     00152000
         L     R5,MAILKTAB   get keyword table                          00153000
         CALL  TBLUK,((R5),RDATE,L'RDATE,TWORK)                         00154000
         LTR   R15,R15       find it?                                   00155000
         BNZ   CKDATE        check for just plain "date:"               00156000
         OI    MAILFLG1,MFLG1RES set resent- flag                       00157000
*[1.16]RESENT-DATE found,  scan down FIEXTN's to find the latest        00158000
*[1.16] date of resending.                                              00159000
         USING FIELDD,R1     address the FIELDD                         00160000
RCK#0    EQU   *                                                        00161000
         CLC   FIEXTN,=F'0'  is this the last occurence of it?          00162000
         BE    RCK#1         out of loop                                00163000
         ICM   R1,B'0111',FIEXTN chain along                            00164000
         B     RCK#0         itterate the search                        00165000
RCK#1    EQU   *             here when last extension found             00166000
         MVC   RESCNT,FIECNT [1.16]set count of # of resent groups      00167000
         DROP  R1            done with FIELDD                           00168000
         B     GOODDATE      go back to old code now                    00169000
CKDATE   EQU   *             [1.16]                                     00170000
**-- FIRST, CHECK FOR EXISTENCE OF 'DATE:'                              00171000
         L     R5,MAILKTAB   GET KEYWORD TABLE                          00172000
         CALL  TBLUK,((R5),DATE,L'DATE,TWORK)                           00173000
         LTR   R15,R15       FIND ONE?                                  00174000
         BZ    GOODDATE                                                 00175000
         TM    MAILOPTS,MOPTDEF        DO THEY WANT DEFAULT DATE        00176000
*                                     PROVIDED                          00177000
         BNO   NODATE        SORRY, COMPLAIN ABOUT DATE MISSING         00178000
         TM    MAILFLG1,MFLG1LCL       AND IS THE FILE LOCAL?           00179000
         BNO   NODATE                                                   00180000
         BAL   R14,FILLDATE  FILL THE DATE FIELD                        00181000
GOODDATE EQU   *                                                        00182000
         ST    R1,MAILDFLD   [1.16]save -> date fieldd                  00183000
         LR    R6,R1         GET FIELDD POINTER                         00184000
         USING FIELDD,R6     GET THE DATE FIELDD                        00185000
         CLC   FIEXTN,=A(0)  ONLY ALLOW 1 OCCURENCE OF "DATE:"          00186000
         BNE   NODATE        SORRY.. YOU CAN'T GIVE 2 DATES...          00187000
         DROP  R6                                                       00188000
         SPACE ,                                                        00189000
*****************begin [1.16]*************************************      00190000
*[1.16]look for RESENT-SENDER first                                     00191000
         L     R5,MAILKTAB   address of keyword table                   00192000
         CALL  TBLUK,((R5),RSENDER,L'RSENDER,TWORK)                     00193000
         LTR   R15,R15       find it                                    00194000
         BZ    RSNDROK       RESENT-sender ok                           00195000
*[1.16]look for RESENT-FROM if resent-sender not found                  00196000
         L     R5,MAILKTAB   get the table                              00197000
         CALL  TBLUK,((R5),RFROM,L'RFROM,TWORK)                         00198000
         LTR   R15,R15       find it?                                   00199000
         BNZ   CKSEND        no RESENT-sender, try plain sender         00200000
RSNDROK  EQU   *             here when we've found a resent sender      00201000
         TM    MAILFLG1,MFLG1RES did we see a resent-date?              00202000
         BNO   NODATE        resent-from but no resent-date is bad      00203000
         USING FIELDD,R1     look at it                                 00204000
RS#0     EQU   *             loop to find last extension                00205000
         CLC   FIEXTN,=F'0'  is this the last extension                 00206000
         BE    RS#1          out of loop                                00207000
         ICM   R1,B'0111',FIEXTN  chain along                           00208000
         B     RS#0          itterate                                   00209000
RS#1     EQU   *             here when last fieldd found                00210000
         CLC   RESCNT,FIECNT compare number of resent-dates seen to     00211000
*                            number of resent-froms seen.               00212000
         BH    NOSNDR        more dates than froms: No sender error     00213000
         BL    NODATE        more froms than dates: No date error       00214000
         B     SNDROK        sender is OK                               00215000
         DROP  R1            done with FIELDD                           00216000
CKSEND   EQU   *             [1.16]here for non-RESENT                  00217000
         TM    MAILFLG1,MFLG1RES is resent-flag set?                    00218000
         BO    NOSNDR        resent-date with no resent-from is bad     00219000
*********************end [1.16]*********************************        00220000
* - LOOK FOR "SENDER:" FIELD FIRST -                                    00221000
         L     R5,MAILKTAB   ADDRESS OF KEYWORD TABLE                   00222000
         CALL  TBLUK,((R5),SENDER,L'SENDER,TWORK)                       00223000
         LTR   R15,R15       FIND IT?                                   00224000
         BZ    SNDROK        YES, A SENDER FIELD WAS FOUND              00225000
* - LOOK FOR "FROM:" FIELD IF "SENDER:" ISN'T THERE -                   00226000
         L     R5,MAILKTAB   ADDRESS OF KEYWORD TABLE                   00227000
         CALL  TBLUK,((R5),FROM,L'FROM,TWORK)                           00228000
         LTR   R15,R15       FIND IT?                                   00229000
         BZ    SNDROK        HAVE A SENDER, NOW CHECK IT                00230000
         TM    MAILOPTS,MOPTDEF        DEFAULT SENDER TO BE PROVIDED    00231000
         BNO   NOSNDR        NO SENDER OR DEFAULT PROVIDED...           00232000
         TM    MAILFLG1,MFLG1LCL       FOR A LOCAL USER                 00233000
         BNO   NOSNDR                                                   00234000
         SPACE ,                                                        00235000
*--------------------------------------------------------------------   00236000
*  SUPPLY DEFAULT SENDER INFORMATION.                                   00237000
*--------------------------------------------------------------------   00238000
         BAL   R14,FILLSEND  FILL IN THE SENDER FIELD                   00239000
SNDROK   EQU   *             R1 = FIELDD BASE                           00240000
         ST    R1,MAILSFLD   SAVE IN MAILBLOK FOR FUTURE USE            00241000
         LR    R6,R1         GET FIELDD POINTER                         00242000
         USING FIELDD,R6     ADDRESS IT                                 00243000
         CLC   FIEXTN,=A(0)  BETTER NOT BE ANY EXTENSIONS OF IT         00244000
         BNE   NOSNDR        SENDER IS NOT SO NICE                      00245000
*----------------------------------------------------------------[1.23] 00246090
*        See if this is an RSCS reject (non-exist node)          [1.23] 00246180
*----------------------------------------------------------------[1.23] 00246270
         TM    MAILFLG1,MFLG1REJ FILE REJECTED BY RSCS?                 00246360
         BO    RSCSREJ       JUST RETURN                                00246450
*----------------------------------------------------------------[1.23] 00246540
*        Look the LOCAL or NETWORK sender up in the INCOMING     [1.23] 00246630
*        table before anything else.                             [1.23] 00246720
*----------------------------------------------------------------[1.23] 00246810
         MVC   NETNODE(8),MAILNODE    source node                [1.23] 00246900
         MVC   NETNODE+8(8),MAILUSER  source user                [1.23] 00246990
         L     R5,MAILVTAB            INCOMING table             [1.23] 00247080
         CALL  TBLUK,((R5),NETNODE,L'NETNODE,TWORK) look         [1.23] 00247170
         LR    R8,R1                  VALD ptr into R8           [1.23] 00247260
         LTR   R15,R15                was it found?              [1.23] 00247350
         BNZ   NOTHERE                nope                       [1.23] 00247440
         USING VALD,R8                look at the VALD           [1.23] 00247530
         ICM   R15,B'1111',VAADDR     user exit supplied?        [1.23] 00247620
         DROP  R8                     done w/vald                [1.23] 00247710
         BZ    VALTRUST               no; trust the sender       [1.23] 00247800
         ST    R11,USERVPAR           set up exit plist          [1.23] 00247890
         ST    R8,USERVPAR+4          "                          [1.23] 00247980
         LA    R1,USERVPAR            point to PLIST             [1.23] 00248070
         BALR  R14,R15                call it                    [1.23] 00248160
         LTR   R15,R15                did they like him?         [1.23] 00248250
         BNZ   VALFAIL                he's a bad guy.            [1.23] 00248340
         B     VALTRUST               he's a good guy.           [1.24] 00248380
*----------------------------------------------------------------[1.23] 00248430
*        Sender not in the table.  If a local user,  that's ok   [1.23] 00248520
*        as long as his address parses ok.  For remote senders,  [1.23] 00248610
*        it's OK only if MOPTAUN allows Unknown senders.         [1.23] 00248700
*----------------------------------------------------------------[1.23] 00248790
NOTHERE  EQU   *                                                 [1.23] 00248880
         TM    MAILFLG1,MFLG1LCL  LOCAL OR NETWORK SOURCE?              00251000
         BNO   NETWORK                from remote sender         [1.23] 00252490
         SPACE ,                                                        00253000
*--------------------------------------------------------------------   00254000
*   FILE IS FROM A LOCAL USER.  VERIFY THAT THE SUPPLIED SENDER         00255000
*   AND THE SENDER IN THE SFBLOK ARE THE SAME USER.                     00256000
*       ( R6->FIELDD )                                                  00257000
*--------------------------------------------------------------------   00258000
LOCUSER  EQU   *                                                        00259000
         L     R5,FIPARS     GET PARSE LIST                             00260000
         USING MBOX,R5     ADDRESS THE ADDRESS DESCRIPTOR               00261590
         CLC   MBXNEXT,=A(0) BETTER NOT BE A LIST OF FROMS              00262180
         BNE   BADLOC                                                   00263000
         CLC   MBXLCNT,=F'1' ONLY 1 TOKEN FOR USER NAME?                00264680
         BNE   BADLOC        NO, NOT A VM USERID...                     00265000
         CLC   MBXDCNT,=F'1' HOST TOKEN COUNT SHOULD BE 1               00266680
         BH    BADLOC        SORRY...                                   00267000
         L     R7,MBXLCL         GET USER NAME NODE                     00269170
         USING NODE,R7     ADDRESS IT                                   00269660
         L     R4,NODDOWN    GET POINTER TO TOKEND                      00270150
         USING TOKEND,R4     GET TOKEN DESCRIPTOR                       00271000
         DROP  R7                                                       00272000
         SR    R7,R7         GET A ZERO                                 00273000
         L     R7,TOKLEN     GET LENGTH OF TOKEN                        00274590
         L     R2,TOKPTR         GET ADDRESS OF STRING                  00275570
         DROP  R4                                                       00276000
         CH    R7,=H'8'      LONGER THAN 8 CHARS?                       00277000
         BH    BADLOC        THAT'S NO GOOD                             00278000
         LA    R3,CLCBUF     ADDRESS TO COPY STRING TO                  00279000
         MVC   CLCBUF,=CL8' ' CLEAR IT FIRST                            00280000
         BCTR  R7,0          -1 FOR EXECUTE                             00281000
         EX    R7,COPY       COPY THE STRING                            00282000
         TR    CLCBUF,UPCASE UPPER CASE IT                              00283000
         CLC   CLCBUF,MAILUSER  THESE BETTER MATCH!!                    00284000
         BNE   BADLOC        NAUGHTY USER!                              00285000
         ICM   R7,B'1111',MBXDML GET HOST NAME NODE                     00287070
         USING NODE,R7     ADDRESS IT                             EAC   00287460
         BNZ   HOSTOK        HOST LIST OK                           EAC 00288000
*-------- HOST NAME OMMITTED.  FILL IN LOCAL NODE NAME AS HOST NAME.EAC 00289000
*-------- BUILD A DUMMY NODE->TOKEND->CPUSTR                      EAC   00290890
         LA    R1,FAKENODE   DUMMY NODE                           EAC   00291780
         ST    R1,MBXDML         STUFF INTO MBOX                 EAC    00292980
         MVC   MBXDCNT,=F'1' SET LENGTH 1                           EAC 00293370
         LR    R7,R1         FIX UP NODE BASE                     EAC   00294490
         LA    R1,CPUSTR     LOCAL MACHINE NAME                     EAC 00295000
         ST    R1,FAKETOK+(TOKPTR-TOKEND) SET TOKEN POINTER             00296680
         LA    R1,CPUSTR+8                                          EAC 00297000
         MVI   TRAPTAB+C' ',X'FF'                                   EAC 00298000
         TRT   CPUSTR,TRAPTAB FIND LENGTH OF STRING                 EAC 00299000
         MVI   TRAPTAB+C' ',X'00'                                   EAC 00300000
         LA    R0,CPUSTR                                            EAC 00301000
         SR    R1,R0         LENGTH OF STRING                       EAC 00302000
         ST    R1,FAKETOK+(TOKLEN-TOKEND)                               00303490
*-------- CHAIN FAKE "AT HOST" ONTO SENDER FIELDD AND SET EDIT FLAG EAC 00304000
         LA    R1,FITOK      GET LIST OF TOKENS                     EAC 00305000
FINDEND  EQU   *                                                    EAC 00306000
         ICM   R2,B'1111',0(R1) GET POINTER TO NEXT ONE             EAC 00307000
         BZ    FOUNDEND      FOUND END OF LIST                      EAC 00308000
         USING TOKEND,R2                                                00309000
         CLI   TOKTYPE,TOKRTB   RIGHT BRACE                             00310490
         BE    FOUNDEND                                                 00311000
         LR    R1,R2         CHAIN ALONG                            EAC 00312000
         B     FINDEND                                              EAC 00313000
FOUNDEND EQU   *                                                    EAC 00314000
*********************************************************************** 00314100
*                                                                     * 00314200
* NOTE:  The following code should be changed to build the TOKEND     * 00314300
*        on the stack (and do 2 way chaining).  This is left as a     * 00314400
*        future project.                                              * 00314500
*                                                                     * 00314600
*********************************************************************** 00314700
         LA    R0,FAKETOK0   ADD INTO CHAIN                             00315000
         STCM  R0,B'0111',1(R1)                                         00316000
         ST    R2,FAKETOK+TOKNEXT-TOKEND                                00317490
         OI    FIFLG1,FIEDIFLG                                          00318000
HOSTOK   EQU   *                                                    EAC 00319000
         DROP  R5                                                       00320000
         L     R4,NODDOWN    GET POINTER TO TOKEND                      00321490
         USING TOKEND,R4     GET TOKEN DESCRIPTOR                       00322000
         DROP  R7                                                       00323000
         SR    R7,R7         GET A ZERO                                 00324000
         L     R7,TOKLEN     GET LENGTH OF TOKEN                        00325590
         L     R2,TOKPTR         GET ADDRESS OF STRING                  00326570
         DROP  R4                                                       00327000
*----------------------------------------------------------------[1.22] 00327030
* If a RSCS-domain is defined and the hostname ends in it, then  [1.22] 00327060
* strip it off since the various tables don't include it.        [1.22] 00327090
*----------------------------------------------------------------[1.22] 00327120
         ICM   R4,B'1111',RSCSDOM no RSCS domain string defined  [1.22] 00327150
         BZ    NORSCSD       so stop wasting time!               [1.22] 00327180
         SR    R5,R5         get a zero                          [1.22] 00327210
         IC    R5,0(R4)      get length of domain string         [1.22] 00327240
         LR    R3,R5         make a copy for later               [1.22] 00327270
         LR    R1,R7         length of token                     [1.22] 00327300
         SR    R1,R5         minus length of domain string       [1.22] 00327330
         BM    NORSCSD       token too short to include it       [1.22] 00327360
         LA    R1,0(R1,R2)   beginning of possible match         [1.22] 00327390
         LA    R4,1(R4)      beginning of domain string          [1.22] 00327420
         SR    R0,R0         0 will contain one character        [1.22] 00327450
         SR    R6,R6         and 6 the other                     [1.22] 00327480
DOMLOOP  EQU   *             loop to compare token end to domain [1.22] 00327510
         IC    R0,0(R1)      get next token character            [1.22] 00327540
         O     R0,=X'00000040' upper case it                     [1.22] 00327570
         IC    R6,0(R4)      get next domain string character    [1.22] 00327600
         O     R6,=X'00000040' upper case it too                 [1.22] 00327630
         CR    R0,R6         compare the upper case characters   [1.22] 00327660
         BNE   DOMNOMAT      do match; exit loop                 [1.22] 00327690
         LA    R1,1(R1)      increment pointer                   [1.22] 00327720
         LA    R4,1(R4)      ditto for domain string             [1.22] 00327750
         BCT   R5,DOMLOOP    loop for length of string           [1.22] 00327780
* falling thru here means they match.                            [1.22] 00327810
         SR    R7,R3         chop off end of string              [1.22] 00327840
NORSCSD  EQU   *             no rscs domain given                [1.22] 00327870
DOMNOMAT EQU   *             fall thru here to old lookup code   [1.22] 00327900
         L     R3,MAILRTAB   LOOK IN ROUTING TABLE FOR AN ALIAS         00328000
         CALL  TBLUK,((R3),(R2),(R7),TWORK)                             00329000
         LTR   R15,R15       FIND IT?                                   00330000
         BNZ   BADLOC        NOPE                                       00331000
         USING ROUTD,R1      ADDRESS THE ENTRY                          00332000
         CLC   RTROUTE,CPUSTR IS IT AN ALIAS FOR THIS NODE?             00333000
         DROP  R1                                                       00334000
         BNE   BADLOC        NOPE                                       00335000
         SR    R15,R15       OK RETCODE                                 00336000
         B     RET                                                      00337000
         SPACE ,                                                        00338000
*----------------------------------------------------------------[1.23] 00341990
*     Get here if we trust the sender because he is in table     [1.23] 00344980
*     but has no exit to call or because his exit says he's OK.  [1.23] 00347970
*----------------------------------------------------------------[1.23] 00350960
VALTRUST EQU   *                                                 [1.23] 00353950
**--- DO SOME STATISTICS                                                00362000
         USING VALD,R8       address the vald                    [1.23] 00362500
         L     R1,VAMSGCNT   GET MSG COUNTER                            00363000
         LA    R1,1(R1)      BUMP SUCCESS COUNTER                       00364000
         ST    R1,VAMSGCNT   STUFF IT BACK                              00365000
         LA    R1,MAILSFB    GET SFB                                    00366000
         USING SFBLOK,R1     ADDRESS                                    00367000
         L     R1,SFBRECNO   GET NUMBER OF RECORDS IN FILE              00368000
         DROP  R1                                                       00369000
         C     R1,VAMSGMAX   BIGGER THAN MAXIMUM?                       00370000
         BL    NOTMAX        NOT THE NEW MAX THEN                       00371000
         ST    R1,VAMSGMAX   SET NEW MAXIMUM                            00372000
NOTMAX   EQU   *                                                        00373000
         A     R1,VAMSGLEN   ADD TO LENGTH TOTAL                        00374000
         ST    R1,VAMSGLEN   THIS IS NEW TOTAL                          00375000
         DROP  R8                                                       00376000
         SR    R15,R15       RETURN OK                                  00377000
RET      EQU   *                                                        00378000
         LTR   R15,R15       some validation error               [1.23] 00378200
         BZ    RET1          nope                                [1.23] 00378400
         OI    MAILFLG2,MFLG2VLF VaLidate Failed                 [1.23] 00378600
RET1     EQU   *                                                 [1.23] 00378800
         L     R13,4(R13)              GET CALLER'S SAVE PTR            00379000
         L     R14,12(R13)             GET R14                          00380000
         LM    R0,R12,20(R13)          GET R0-R12                       00381000
         BR    R14                     RETURN                           00382000
         SPACE ,                                                        00383000
*--------------------------------------------------------------------   00384000
*        ERROR EXITS                                                    00385000
*--------------------------------------------------------------------   00386000
NOSNDR   EQU   *             DIDN'T GIVE FROM OR SENDER                 00387000
         WTR   VAL020                                                   00388000
         LA    R15,4                                                    00389000
         B     RET                                                      00390000
         SPACE ,                                                        00391000
BADLOC   EQU   *             LOCAL USER GAVE BAD ADDRESS                00392000
         WTR   VAL021                                                   00393000
         LA    R15,4                                                    00394000
         B     RET                                                      00395000
         SPACE ,                                                        00396000
*----------------------------------------------------------------[1.23] 00397090
*    Get here if sender is not in the table and file is from     [1.23] 00397180
*    the network.  Check to see if Allow-Unknown-Nodes is true   [1.23] 00397270
*    and try the *DEFAULT* entry in the table before giving up   [1.23] 00397360
*    on him.                                                     [1.23] 00397450
*----------------------------------------------------------------[1.23] 00397540
NETWORK  EQU   *             UNKNOWN NETWORK SENDER              [1.23] 00397630
*  SEE IF THE ALLOW-UNKNOWN-NODES FLAG IS ON. IF SO, IGNORE THIS        00398000
*  ERROR.                                                               00399000
         TM    MAILOPTS,MOPTAUN        CHECK FLAG                       00400000
         BNO   BADNET1       IT'S OFF, DON'T LET THE FILE IN            00401000
         CALL  TBLUK,((R5),DEFAULT,L'DEFAULT,TWORK)                     00402000
         LR    R8,R1         GET A BETTER HANDLE                        00403000
         LTR   R15,R15       FOUND IT?                                  00404000
         BZ    DEFAUN        DO DEFAULT UNKNOWN NODE PROCESSING         00405000
         SR    R15,R15       ALLOW IT WITH NO VALIDITY CHECKER          00406000
         B     RET           CLEAN EXIT                                 00407000
BADNET1  EQU   *                                                        00408000
         LA    R4,MAILNODE   NODE NAME                                  00409000
         LA    R5,MAILUSER   USER ID                                    00410000
         WTR   VAL019,(CA,(R4),CA,(R5))                                 00411000
         LA    R15,4                                                    00412000
         B     RET                                                      00413000
DEFAUN   EQU   *                      here if MOPTAUN works      [1.23] 00414040
         USING VALD,R8                look at the VALD           [1.23] 00414080
         ICM   R15,B'1111',VAADDR     user exit supplied?        [1.23] 00414120
         DROP  R8                     done w/vald                [1.23] 00414160
         BZ    VALTRUST               no; trust the sender       [1.23] 00414200
         ST    R11,USERVPAR           set up exit plist          [1.23] 00414240
         ST    R8,USERVPAR+4          "                          [1.23] 00414280
         LA    R1,USERVPAR            point to PLIST             [1.23] 00414320
         BALR  R14,R15                call it                    [1.23] 00414360
         LTR   R15,R15                did they like him?         [1.23] 00414400
         BZ    VALTRUST               yes; trust the sender      [1.23] 00414440
*      fall thru here;  don't trust him                          [1.23] 00414480
*----------------------------------------------------------------[1.23] 00414520
*    Get here if sender is in table and his user exit doesn't    [1.23] 00414560
*    like him.                                                   [1.23] 00414600
*----------------------------------------------------------------[1.23] 00414640
VALFAIL  EQU   *             USER VALIDATION FAILED                     00415000
         USING VALD,R8                                                  00416000
         L     R1,VAMSGFAI   GET FAILURE COUNT                          00417000
         LA    R1,1(R1)      UP IT ONE                                  00418000
         ST    R1,VAMSGFAI   AND PUT BACK INTO FAILURE COUNT            00419000
         DROP  R8                                                       00420000
         LR    R5,R15        GET RC                                     00421000
         WTR   VAL024        ISSUE MESSAGE                              00422000
         LA    R15,4         MINOR ERROR                                00423000
         B     RET           GO DIE                                     00424000
         SPACE ,                                                        00425000
NOSPACE  EQU   *                                                        00426000
         WTR   VAL003,(CA,MYNAME)                                       00427000
         LA    R15,8                                                    00428000
         BR    R14                                                      00429000
         SPACE ,                                                        00430000
NODATE   EQU   *             NO DATE SUPPLIED                           00431000
         WTR   VAL023        ISSUE THE MESSAGE                          00432000
         LA    R15,4         RETURN MINOR ERROR                         00433000
         B     RET           AND RETURN                                 00434000
         SPACE ,                                                        00435000
RSCSREJ  EQU   *                                                        00436000
         LA    R15,4         ERROR RETURN                               00437000
         B     RET                                                      00438000
         SPACE                                                          00439000
HOPCOUNT EQU   *             <- too many received: lines         [1.24] 00439100
         WTR   VAL063        write error message                 [1.24] 00439200
         LA    R15,4         return minor error                  [1.24] 00439300
         B     RET           -> and return                       [1.24] 00439400
         SPACE ,                                                 [1.24] 00439500
*begin [1.18]-----                                                      00440000
*        ALWAYS send xfer'd mail to the maintainer.  It would           00441000
*        certainly confuse the originator to get the error mail         00442000
*        because one of his recipients was dumb enough to transfer      00443000
*        the file back to mailer.                                       00444000
*-------------------------------------------------------------------    00445000
BADXFER  EQU   *                                                        00446000
BADUXFER EQU   *                       <- so I'm lazy...         [1.24] 00446500
         NI    MAILFLG1,X'FF'-MFLG1HDS say header was invalid (lie)     00447000
         MVC   MAILUSER,MAINTID        always send to maintainer        00448000
         MVC   MAILNODE,MAINTNOD       ...                              00449000
         WTR   VAL048                                                   00450000
         LA    R15,4                                                    00451000
         B     RET                                                      00452000
*end [1.18]-------                                                      00453000
         EJECT ,                                                        00454000
*--------------------------------------------------------------------   00455000
*        ROUTINES TO SUPPLY DEFAULT VALUES FOR DATE AND SENDER          00456000
*        FIELDS OF THE HEADER.  THESE ROUTINES ARE INVOKED IF           00457000
*        THE INCOMING MAIL FILE WAS LACKING THE FIELD AND THE           00458000
*        SUPPLY-MISSING-FIELDS FLAG WAS TRUE.                           00459000
*        A FIELDD, TOKEND AND TEXT STRINGS ARE ALLOCATTED ON THE        00460000
*        STACK FOR THESE FIELDS AND TBADD IS CALLED TO ADD THEM         00461000
*        TO THE MAILKTAB.                                               00462000
*--------------------------------------------------------------------   00463000
         SPACE ,                                                        00464000
FILLDATE EQU   *                                                        00465000
         BR    R14                                                      00466000
         SPACE ,                                                        00467000
FILLSEND EQU   *                                                        00468000
         BR    R14                                                      00469000
         SPACE ,                                                        00470000
COPY     MVC   0(0,R3),0(R2)                                            00471000
         LTORG ,                                                        00472000
SENDER   DC    C'SENDER'                                                00473000
FROM     DC    C'FROM'                                                  00474000
DATE     DC    C'DATE'                                                  00475000
RSENDER  DC    C'RESENT-SENDER'        [1.16]                           00476000
RFROM    DC    C'RESENT-FROM'          [1.16]                           00477000
RDATE    DC    C'RESENT-DATE'          [1.16]                           00478000
DEFAULT  DC    0CL16' ',CL8'*DEFAULT*',CL8'*DEFAULT*'                   00479000
SAVE     DS    18F                     SAVE AREA                        00480000
TWORK    DS    64F                                                      00481000
ADSTR    DS    2F                                                       00482000
ADSTK    DS    2F                                                       00483000
TOKLIST  DS    F                                                        00484000
PARSLIST DS    F                                                        00485000
USERVPAR DS    A             USER EXIT PARM LIST                        00486000
NETNODE  DC    CL16' '                                                  00487000
CLCBUF   DS    CL8                                                      00488000
         DS    0D                                                       00489590
FAKENODE DC    (NODSIZ)X'0'                                             00490180
         ORG   FAKENODE                                                 00490770
         DC    F'0'          NODNEXT is zero                            00491360
         DC    F'0'          NODPREV is zero                            00491950
         DC    A(FAKETOK)    POINT TO DUMMY TOKEND                      00493000
         SPACE 1                                                        00493600
FAKETOK  DS    (TOKSIZ)X'0'                                             00494290
         ORG   FAKETOK                                                  00494580
         DC    A(0)          No next token                              00494870
         DC    A(0)          No previous token                          00495160
         DC    A(0)          No string pointer                          00495450
         DC    F'0'          Length of zero                             00495740
         DC    AL1(TOKATM)   Atom type token                            00496030
         ORG   ,                                                        00496320
         SPACE 1                                                        00496610
FAKETOK0 DS    (TOKSIZ)X'0'                                             00496900
         ORG   FAKETOK0                                                 00497190
         DC    A(FAKETOK)    Next token                                 00497480
         DC    A(0)          No previous token                          00497770
         DC    A(ATSIGN)     String pointer                             00498060
         DC    F'1'          String length                              00498350
         DC    AL1(TOKAT)    Token is at sign                           00498640
         ORG   ,                                                        00498930
         SPACE 1                                                        00499220
ATSIGN   DC    CL1'@'                                      [1.16]       00501000
         END   VALIDATE                                                 00502000
